# Maintainer: Samsagax <samsagax@gmail.com>

pkgname=dkms-bbswitch-git
pkgver=20120112
_modver=0.2
pkgrel=1
pkgdesc="kernel module allowing to switch dedicated graphics card on Optimus laptops, dkms version"
arch=('i686' 'x86_64')
url=("http://github.com/Bumblebee-Project/bbswitch")
license=('GPL')
provides=('bbswitch')
conflicts=('bbswitch-git')
depends=('dkms')
makedepends=('git')
source=('dkms.conf.in')
md5sums=('262c8a723584860fa86f8e5fc8a4889a')
install=dkms-bbswitch.install
_gitroot=('git://github.com/Bumblebee-Project/bbswitch.git')
_gitname=("bbswitch")

build() {
  cd ${srcdir}

 ## Git checkout
  if [ -d ${srcdir}/${_gitname} ] ; then
    msg "Git checkout:  Updating existing tree"
    cd ${_gitname} && git pull origin
    msg "Git checkout:  Tree has been updated"
  else
    msg "Git checkout:  Retrieving sources"
    git clone ${_gitroot}
  fi
  msg "Checkout completed"
}

package() {
  cd ${srcdir}/${_gitname}
  install -dm755 "${pkgdir}/usr/src/bbswitch-${_modver}/"
  for i in "${srcdir}/${_gitname}/"{Makefile,bbswitch.c}; do
    install -D -m644 "${i}" "${pkgdir}/usr/src/bbswitch-${_modver}/"
  done
  sed "s/#MODULE_VERSION#/${_modver}/" "${srcdir}/dkms.conf.in" > "${pkgdir}/usr/src/bbswitch-${_modver}/dkms.conf"
}
