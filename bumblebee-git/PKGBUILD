# This script is incomplete and in development.

# Maintainer: Samsagax <samsagax@gmail.com>

pkgname=bumblebee-git
pkgver=20120114
pkgrel=1
pkgdesc="Develop branch of Bumblebee. Optimus Support for Linux Through VirtualGL. You need to install proper drivers separately. Can be used with Nouveau or Nvidia"
arch=('i686' 'x86_64')
makedepends=('git')
depends=('virtualgl' 'libbsd' 'glib2')
optdepends=('xf86-video-nouveau: Nouveau driver' 'nouveau-dri: 3D acceleration features fo Nouveau' 'mesa: 3D acceleration features fo Nouveau'  'bbswitch: switch on/off discrete card' 'nvidia-utils-bumblebee: Nvidia utils not breaking LibGL' 'nvidia: Nvidia kernel driver')
if [ "$CARCH" = "x86_64" ]; then
     optdepends[${#optdepends[@]}]='lib32-virtualgl: run 32bit applications with optirun'
fi
url="https://github.com/Bumblebee-Project/bumblebeed"
license=("GPL3")
install='bumblebee.install'
conflicts=('bumblebee')
provides=('bumblebee')
backup=('etc/bumblebee/bumblebee.conf')
source=('bumblebeed.in')
md5sums=('4ccc7eb0e1141dac18812586d27c8fb6')

_gitroot="git://github.com/Bumblebee-Project/bumblebeed.git"
_gitname="bumblebeed"
_gitbranch="release-3.0"

build() {
    cd "$srcdir"
    msg "Connecting to GIT server...."
    if [ -d $_gitname ] ; then
        cd $_gitname && git pull origin
        msg "The local files are updated."
    else
        git clone "$_gitroot" "$_gitname" -b "$_gitbranch"
        cd "$_gitname"
    fi
    msg "GIT checkout done or server timeout"

    autoreconf -fi
    ./configure \
        CONF_DRIVER_MODULE_NVIDIA=nvidia \
        CONF_LDPATH_NVIDIA=/usr/lib/nvidia-bumblebee:/usr/lib32/nvidia-bumblebee \
        CONF_MODPATH_NVIDIA=/usr/lib/nvidia-bumblebee/xorg/,/usr/lib/xorg/modules \
        --prefix=/usr \
        --sysconfdir=/etc
    make
}

package() {
    cd "$srcdir/$_gitname"
    make install DESTDIR="$pkgdir"
    install -D -m755 "$srcdir/bumblebeed.in" "$pkgdir/etc/rc.d/bumblebeed"
}
