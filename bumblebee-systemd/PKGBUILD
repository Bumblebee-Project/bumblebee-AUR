# This script is incomplete and in development.

# Maintainer: Samsagax <samsagax@gmail.com>

pkgname=bumblebee-systemd
_pkgbase=bumblebee
pkgver=3.0
pkgrel=1
pkgdesc="Bumblebee brings Optimus Support for Linux Through VirtualGL. You need to install proper drivers separately. Can be used with Nouveau or Nvidia with systemd unit"
arch=('i686' 'x86_64')
depends=('virtualgl' 'glib2')
optdepends=('xf86-video-nouveau: Nouveau driver' 'nouveau-dri: 3D acceleration features fo Nouveau' 'mesa: 3D acceleration features fo Nouveau'  'bbswitch: switch on/off discrete card' 'nvidia-utils-bumblebee: Nvidia utils not breaking LibGL' 'nvidia: Nvidia kernel driver')
if [ "$CARCH" = "x86_64" ]; then
     optdepends[${#optdepends[@]}]='lib32-virtualgl: run 32bit applications with optirun'
fi
url="http://www.Bumblebee-Project.org"
license=("GPL3")
install='bumblebee.install'
provides=('bumblebee')
backup=('etc/bumblebee/bumblebee.conf')
source=("https://github.com/downloads/Bumblebee-Project/Bumblebee/${_pkgbase}-${pkgver}.tar.gz")
md5sums=('2ddd4d47bc52aceb3b295e68f9f32ffe')

build() {
    cd "${srcdir}/${_pkgbase}-${pkgver}"
    ./configure \
        CONF_DRIVER_MODULE_NVIDIA=nvidia \
        CONF_LDPATH_NVIDIA=/usr/lib/nvidia-bumblebee:/usr/lib32/nvidia-bumblebee \
        CONF_MODPATH_NVIDIA=/usr/lib/nvidia-bumblebee/xorg/,/usr/lib/xorg/modules \
        --prefix=/usr \
        --sysconfdir=/etc \
        --without-pidfile
    make
}

package() {
    cd "${srcdir}/${_pkgbase}-${pkgver}"
    make install DESTDIR="$pkgdir"
    install -D -m755 "scripts/systemd/bumblebeed.service" "$pkgdir/usr/lib/systemd/bumblebeed.service"
    # Make bash_completion work
    mv -v "${pkgdir}/etc/bash_completion.d/bumblebee" "${pkgdir}/etc/bash_completion.d/optirun"
}
